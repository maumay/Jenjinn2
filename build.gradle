apply plugin: 'java'
apply plugin: 'eclipse'
sourceCompatibility = 1.8

repositories {
    mavenCentral()

    flatDir {
    	dirs 'libs'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
	tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
    }
 }

dependencies {
	if (subprojects.isEmpty()) {
		compile name: 'JFlow-0.4'
	}
	else {
		def asList = new ArrayList<>(subprojects)
		compile project(asList.get(0).name)
	}
	
	// Test dependencies.
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

jar {
    manifest {
        attributes(
        	'Implementation-Title': 'Jenjinn',
            'Implementation-Version': '2.0',
            'Class-Path': configurations.compileClasspath.files.collect {"$it.name"} .join(' '),
            )
    }
    
    from {
         configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


task wrapper(type: Wrapper) {
	gradleVersion = '4.6'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
